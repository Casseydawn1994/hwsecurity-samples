apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "de.cotech.hw.fido.example"
        minSdkVersion 21
        targetSdkVersion 29

        // insert SDK version used to build this apk
        versionName "4.2.1"
        versionCode 20

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = "1.4"
    }

    if (project.hasProperty('fidoSigningStoreLocation') &&
            project.hasProperty('fidoSigningStorePassword') &&
            project.hasProperty('fidoSigningKeyAlias') &&
            project.hasProperty('fidoSigningKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"

        signingConfigs {
            release {
                storeFile file(fidoSigningStoreLocation)
                storePassword fidoSigningStorePassword
                keyAlias fidoSigningKeyAlias
                keyPassword fidoSigningKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0-alpha06'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.webauthn4j:webauthn4j-core:0.14.1.RELEASE'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.4"

    implementation project(':hwsecurity:core')
    implementation project(':hwsecurity:fido')
    implementation project(':hwsecurity:fido-util')
    implementation project(':hwsecurity:fido2')

    implementation 'io.sentry:sentry-android:2.3.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
